#include "common.cginc"

sampler2D inputTexture : register(s0) = {
	MipFilter = POINT,
    MinFilter = POINT,
    MagFilter = POINT,
	AddressU = CLAMP,
	AddressV = CLAMP,
};
 
sampler2D depthTexture : register(s1) = {
	MipFilter = POINT,
    MinFilter = POINT,
    MagFilter = POINT,
	AddressU = CLAMP,
	AddressV = CLAMP,
};

// TODO PACKING
float4x4	invViewProj							 : register(c0);
float3		camPos								 : register(c4);
float		invRetinaRadiusProductInputTexWidth  : register(c5);
float		invTexWidth							 : register(c6);
float		invTexHeight						 : register(c7);
float		MinusInvTexWidth					 : register(c8);
float		MinusInvTexHeight					 : register(c9);
float		aperture							 : register(c10);
float		retinaLensDist						 : register(c11);

struct VS_IN {
	float3 posL 	: POSITION;
	float2 tex0 	: TEXCOORD0;
};

struct VS_OUT {
	float4 posH 	: SV_POSITION;
	float2 tex0 	: TEXCOORD2;
};


VS_OUT VS_MAIN(VS_IN In, uint VertexID : VERTEXID) {
	VS_OUT Out;
	Out.tex0 = float2( (VertexID << 1) & 2, VertexID & 2 );
	Out.posH = float4(  Out.tex0 * float2( 2.0f, -2.0f ) + float2( -1.0f, 1.0f), 0.0f, 1.0f );
	return Out;
}

float4 PS_MAIN(VS_OUT In) : COLOR0 {
	float focusedPointDist = distance(camPos, GetWorldPosition( In.tex0, tex2D(depthTexture, float2(0.5f, 0.5f)), invViewProj));
	float focalLength = retinaLensDist * focusedPointDist / (retinaLensDist + focusedPointDist);
	float pointDist = distance(camPos, GetWorldPosition( In.tex0, tex2D(depthTexture, In.tex0), invViewProj));

	float CoC = abs(aperture * focalLength * (focusedPointDist - pointDist) * (1.0f / ( pointDist * (focusedPointDist - focalLength))));

	// Interesting example : CoC * (1.0f / ~retinaRadius) * inputTextureWidth;
	// The first product normalizes te CoC to our retine, so the result will be [0,1] then scale this by input texture width
	float blurRadius = CoC * invRetinaRadiusProductInputTexWidth;

	float4 color = tex2D(inputTexture, In.tex0);
	float alpha = color.a;

	float quality = 6; 
	for( float i = 1; i < quality; ++i)
	{
		float blurAmount = i  / quality * blurRadius;
		color += tex2D(inputTexture,  float2(  invTexWidth		,  0				 ) * blurAmount + In.tex0);
		color += tex2D(inputTexture,  float2(  0				,  invTexHeight		 ) * blurAmount + In.tex0);
		color += tex2D(inputTexture,  float2(  0				,  MinusInvTexHeight ) * blurAmount + In.tex0);
		color += tex2D(inputTexture,  float2(  MinusInvTexWidth ,  0				 ) * blurAmount + In.tex0);
	}

	float4 finalColor = color * (1.0f / (quality * 4 + 1));
	finalColor.a = alpha;

	return finalColor;
}

technique t
{
	pass p
	{
		VertexProgram = compile vs_4_0 VS_MAIN();
		FragmentProgram = compile ps_4_0 PS_MAIN();
	}
}