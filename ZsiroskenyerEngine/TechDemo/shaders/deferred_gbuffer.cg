#include "common.cginc"

// texture maps
sampler2D diffuseMap = {
	MipFilter = LINEAR,
    MinFilter = LINEAR,
    MagFilter = LINEAR,
	AddressU = WRAP,
	AddressV = WRAP,
};

sampler2D normalMap = {
	MipFilter = LINEAR,
    MinFilter = LINEAR,
   	MagFilter = LINEAR,
	AddressU = WRAP,
	AddressV = WRAP,
};

sampler2D glossinessMap = {
	MipFilter = LINEAR,
    MinFilter = LINEAR,
    MagFilter = LINEAR,
	AddressU = WRAP,
	AddressV = WRAP,
};

sampler2D specLevelMap = {
	MipFilter = LINEAR,
    MinFilter = LINEAR,
    MagFilter = LINEAR,
	AddressU = WRAP,
	AddressV = WRAP,
};


#include "deferred_gbuffer_material.cginc"


//------------------------------------------------------------------------------
//	Shader constants
//------------------------------------------------------------------------------
struct {
	float4x4 worldViewProj;
	float4x4 worldView;
	float4x4 world;
	float3 camPos;
	float near, far;
} transform : register(c0);


//------------------------------------------------------------------------------
// Shader in/out structures
//------------------------------------------------------------------------------
struct VS_IN {
	float3 posL 	: POSITION;
	float3 normalL 	: NORMAL;
	float3 tangentL : COLOR0;
	float2 tex0 	: TEXCOORD0;
};

struct VS_OUT {
	float4 posH 	: SV_POSITION;
	float3 normalW 	: TEXCOORD0;
	float3 tangentW : TEXCOORD3;
	float2 tex0 	: TEXCOORD1;
	float3x3 TBN	: TEXCOORD2;
	float linearDepth : COLOR0;
};


//------------------------------------------------------------------------------
// Vertex shader
//------------------------------------------------------------------------------
VS_OUT VS_MAIN(VS_IN In) {
	VS_OUT Out;

	Out.posH = 
		In.posL.x * transform.worldViewProj[0] + (
		In.posL.y * transform.worldViewProj[1] + (
		In.posL.z * transform.worldViewProj[2] + 
		transform.worldViewProj[3]));
	Out.normalW = 
		In.normalL.x * transform.world[0].xyz + (
		In.normalL.y * transform.world[1].xyz + (
		In.normalL.z * transform.world[2].xyz));
	Out.tangentW = 
		In.tangentL.x * transform.world[0].xyz + (
		In.tangentL.y * transform.world[1].xyz + (
		In.tangentL.z * transform.world[2].xyz));

	float viewZ = transform.worldView[0][2] * In.posL.x + 
				  transform.worldView[1][2] * In.posL.y + 
				  transform.worldView[2][2] * In.posL.z + 
				  transform.worldView[3][2];
	Out.linearDepth = (viewZ - transform.near)/(transform.far - transform.near);

	Out.tex0 = In.tex0;

	// Calculate TBN base per vertex (CHEAP) (Tangent to World space)
	float3 N = normalize(Out.normalW);
	float3 T = normalize(Out.tangentW - dot(Out.tangentW,N) * N);
	float3 B = normalize(cross(N,T));
	Out.TBN = float3x3(T,B,N);
	return Out;
}


//------------------------------------------------------------------------------
// Vertex shader
//------------------------------------------------------------------------------
struct PS_OUT {
	float4 gb0 : COLOR0;
	float2 gb1 : COLOR1;
	float4 gb2 : COLOR2;
	float depth : DEPTH;
};
PS_OUT PS_MAIN(VS_OUT In) {
	PS_OUT Out;
	GBUFFER gb;
	if (mtlProperties.hasNormalMap) {
		gb = PixelShader_NormalMap(In.TBN, In.tex0);
		Out.gb0 = gb.diffuse;
		Out.gb1 = gb.normal;
		Out.gb2 = gb.misc;
	}
	else {
		gb = PixelShader_Simple(In.normalW, In.tex0);
		Out.gb0 = gb.diffuse;
		Out.gb1 = gb.normal;
		Out.gb2 = gb.misc;
	}
	Out.depth = In.linearDepth;
	return Out;
};

technique t
{
	pass p
	{
		VertexProgram = compile vs_4_0 VS_MAIN();
		FragmentProgram = compile ps_4_0 PS_MAIN();
	}
}